set grid
path = "e:/projects/lababins/visual/"
r = 6371

set parametric
set angle degree
set urange [0:360]
set vrange [-90:90]
set isosample 10,10

set term pngcairo enhanced size 1360,1000
set output path."track.png"

set xlabel "x,km"
set ylabel "y,km"
set zlabel "z,km"

set xrange [*:*]
set yrange [*:*]

splot   path."results.txt" u 1:2:3 t"true" with points lc rgb"red",\
		path."results.txt" u 7:8:9 t"bins" with points lc rgb"green",\
		r*cos(u)*cos(v),r*sin(u)*cos(v),r*sin(v) t"Earth" with lines lc rgb"black"
		

set xlabel "time, sec" 

labels = "x y z vx vy vz" 
dimension = "m m m m/sec m/sec m/sec"

do for [i=1:6] {
	set xrange [*:*]
	set yrange [*:*]

	label = word(labels,i)
	dimn = word(dimension,i)

	set ylabel "error ".label.", ".dimn 
	set output path."err_".label.".png"
	plot path."results.txt" using 13:(1000*(column(i) - column(i+6))) notitle with lines lc rgb"red"
}

set xrange [*:*]
set yrange [*:*]

set output path."err_position.png"
set ylabel "Position error , m" 
plot path."results.txt" u 13:(1000*sqrt(($7-$1)*($7-$1) + ($8-$2)*($8-$2) + ($9-$3)*($9-$3))) notitle

set output path."err_velocity.png"
set ylabel "Velocity error , m/sec" 
plot path."results.txt" u 13:(1000*sqrt(($10-$4)*($10-$4) + ($11-$5)*($11-$5) + ($12-$6)*($12-$6))) notitle

              
set output 
